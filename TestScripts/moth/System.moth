namespace Moth;

public class Sys {

    static Write(string #str, end #char) #void {
        printf("%s%c", string, end);
        return;
    }

    static WriteLine(string #str) #void {
        #Sys.Write(string, '\n');
        return;
    }

    static GetChar(file #void*) #char {
        local ch #i32 = fgetc(file);

        if !eof(ch) {
            return #char <- ch;
        } else {
            return '\0';
        }
    }

    static ReadLine(file #void*) #str {
        local size #u32 = 512;
        local string #str = malloc(size);
        local ch #char = #Sys.GetChar(file);
        local len #i32 = -1;

        if string == null {
            return string;
        }

        while ch != '\0' {
            string[++len] = ch;
            
            if len >= #i32 <- size {
                string = realloc(string, size = size + #u32 <- 512);

                if string == null {
                    return string;
                }
            }

            if ch == '\n' {
                ch = '\0';
            } else {
                ch = #Sys.GetChar(file);
            }
        }

        string[++len] = ch;
        string = realloc(string, ++len);
        return string;
    }

    static Input(string #str) #str {
        #Sys.Write(string, '\0');
        return #Sys.ReadLine(stdin());
    }
}